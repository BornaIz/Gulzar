Include "shared.fez";
LoadPlugin qalamTools.SeparateConsecutive;

# And now we do some more positioning. We put this in mkmk because we want
# this to be processed at the end, after anchor positioning is done.
Feature mkmk {

  # Dots and marks on the letter preceding a swash kaf need to be dropped
  # down to avoid collision. Dots get moved right slightly.
  # XXX ideally tall kaf/gaf and short kaf/gaf should be treated differently.
  DefineClass @kafgaf = /^(KAF|GAF)/;
  DefineClass @shortkafgaf = @kafgaf & (yMax < 700);
  DefineClass @narrowmedi = /.*m\d+$/  & (run < 350);

  # Dots in a bari ye sequence, created by the BYMoveDots rule above, should
  # also be separated.
  DefineClass @behs = /.*[mif](sd)?\d+$/;

};

Feature kern {
  Routine GiveChYeDotsMoreSpace {
    Position ( [ddb.yb tdb.yb] <80 -100 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( sdb.yb <105 0  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <125 0  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <125 0  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb$/ <20 0 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb$/ <60 0 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <60 0 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) @medis JIMf1;
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) JIMf1;
  } UseMarkFilteringSet /haydb|sdb.yb|tdb|ddb|haydb/;

  DefineClass @WideFinals = @finas & (xMin < 50);

  Routine Various1 {
    # Drop Dots Before Vao
    Position ( [tdb ddb] <yPlacement=-100> ) VAOf1;

    # Raise dots after wide finals
    Position @WideFinals @inits ( [ddb tdb] <yPlacement=+150> ) ;

    # Finesse Beh Haya
    Position /BE(sd)?i/ ([ddb tdb] <xPlacement=50>) /HAYAm/;

    # Finesse Beh JIM
    Position ( /ddb|tdb/ <xPlacement=+20> ) /JIMm/;
  } UseMarkFilteringSet /tdb|ddb$/;

  Routine KernBeHaya {
    Position (space <xAdvance=-100> ) /[BT]Ei/ /HAYA/;
  } IgnoreMarks;

  DefineClass @NotTooHighStuff = (@finas|@isols) & (yMax < 600);
  DefineClass @NarrowMedials = /[mf](sd)?\d+$/ & (run < 350);

  Routine Various2 {
    # GrossKasraAvoidanceThing
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-200> ) AINf1;
    Position ( KASRA.yb <yPlacement=+200> ) AINf1;
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-400> ) JIMf1;

    # Raise dots on final JIM
    Position /[mi]\d+$/ [sdb ddb tdb] JIMf1 ([tdi sdi] <yPlacement=150> );

    # Add space around pointy damma
    Position @NotTooHighStuff DAMMA (space <xAdvance=+50> );

    # Drop haydb JIMf1

    # Finess Init kasra yeh barree e.g. رِیے
    Position KASRA ( @inits <xAdvance=+200> ) /.yb/;

    # Clear Alif Madda
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+160> ) @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+160> ) @below_dots @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+100> );

    # Clear Alif Nun KL
    Position /ALIF[uf]1/ (BEi8 <xAdvance=+50>) /sda|dda/ KAFm3;

    # Dots under kaf
    Position (/toeda|tda|DAMMA/ <yPlacement=-50>) /KAF|GAF/;
    Position (/toeda|tda|DAMMA/ <yPlacement=-130>) @NarrowMedials /KAF|GAF/;
    Position (/toeda|tda|DAMMA/ <yPlacement=-130>) @NarrowMedials @all_above_marks /KAF|GAF/;
    Position (/sda|dda/ <yPlacement=-50>) @NarrowMedials /KAF|GAF/;
    Position (/sda|dda/ <yPlacement=-50>) @NarrowMedials @all_above_marks /KAF|GAF/;
    Position /FE[im]/ (/sda|dda/ <yPlacement=-100>) /KAF|GAF/;

    # Lam Khah
    Position /LAM[im]/ /JIM[mf]/ ([sda sda.one] <xPlacement=-50>);

    # Just fiddling now
    Position  parenright.latin (/BEi/ <xAdvance=+70>) [dda tdb];

  };

  DefineClass @TallAndStraight = /LAM[im]|[KG]AF[im]|JIM[im]/;
  DefineClass @LowNotTeh = /^(?!TE|LAM|[KG]AF|ALIF)\w+[mf]\d+/;
  DefineClass @NarrowTeh = /^TE[mf](sd)?\d+/ & (run < 300);
  DefineClass @TallFinal = /LAM[mf]|[KG]AF[mf]|JIM[mf]|ALIFf1/;
  Routine HighTehFinesse {
    Position @TallAndStraight /TEm|JIMm/ ( [sda tda dda] <xPlacement=-150> ) /_YEf/;
  }  UseMarkFilteringSet [sda dda tda];

  Routine TdbAinFinesse {
    Position (tdb.yb <xPlacement=-30>) AINf1;
  } UseMarkFilteringSet [tdb.yb];

  Routine InitChechFinesse {
    Position ( /JIMi/ <xAdvance=+50> tdb <xPlacement=+50> );
  };

  Routine FeTeFinesse {
    Position /FEi/ /TEm/ ( dda <yPlacement=+50> ) ;
    Position /FEi/ dda /TEm/ ( dda <yPlacement=+50> ) ;
  } UseMarkFilteringSet [dda];


  Routine AddIsolDammaSpace {
    Position [@isols @finas] DAMMA ( @inits <xAdvance=+100> );
  } UseMarkFilteringSet [DAMMA];


};
