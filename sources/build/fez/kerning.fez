LoadPlugin qalamTools.NastaliqKerning;
Include "shared.fez";

DefineClass @bariye = /BARI_YE/;

LoadPlugin FontEngineering;
SetCategory space.urdu ligature;


Feature kern {
	NastaliqKerning 100 70%;

	Routine FixShortKern {
		Position [REf2 REf3] ([BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] <xAdvance=+400>); # مربی / عربی
		Position @alif_like ([BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] <xAdvance=+50>); # ابی
		
		Position @dal_like ([BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] <xAdvance=+200>); # دبی
		Position @vao_like ([BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] <xAdvance=+200>); # وبی
		Position @mim_like ([BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] <xAdvance=+200>); # م بی

		Position /(CH_YE)[uf]/ ([BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] <xAdvance=+300>); # یی پی

		Position @vao_like    (/[BTF]Ei/ <xAdvance=+300>) /JIMf|AINf/; #  سوئچ
		Position @mim_like    (/[BTF]Ei/ <xAdvance=+300>) /JIMf|AINf/; #  سم ئچ
		Position @bigbowlfina (/[BTF]Ei/ <xAdvance=+300>) /JIMf|AINf/; #  سن ئچ
		Position @dal_like    (/[BTF]Ei/ <xAdvance=+300>) /JIMf|AINf/; #  ریخ

	} IgnoreLigatures IgnoreMarks;

	Routine FixShortKern2 {
		Position /ALIF/ (/[KG]AFi|LAMi|TEi|BEi|FEi|MIMi/ <xAdvance=+100>) /CH_YEf|JIM[fm]|AINf|MIMf/;
		Position /ALIF/ (/BEi1$|BEisd1$/ <xAdvance=+75>) /[sdt]db/;
		# Position /ALIF/ (/[KG]AFi|LAMi|TEi|BEi|FEi|MIMi/ <xAdvance=+60>);
		Position /(DAL|RE|VAO|ALIF)[uf]/ (/[KG]AFi|LAMi/ <xAdvance=+300>) /(JIM|BE)m/ /[sdt]db/;
	} IgnoreLigatures UseMarkFilteringSet /[sdt]db/;

	Routine FixShortKern3 {
		Position /ALIF/ (/HAYCi/ <xAdvance=+100>) /haydb/;
		Position /REf3/ (/TEi/ <xAdvance=+100>);
	} IgnoreLigatures UseMarkFilteringSet /dda|sdb|haydb/;

	Routine OtherKerningFixes {
		Position REf3 (/[KG]AFi/ <xAdvance=-120>);
		Position @vao_like (space.urdu <xAdvance=50>) /AINi/;
		Position @bigbowlfina (space.urdu <xAdvance=150>) /JIMi/ /[sdt]db/;
		Position @bigbowlfina (space.urdu <xAdvance=100>) /AINi/;
		Position [REf2 REf3] (space.urdu <xAdvance=150>) /JIMi/;
		Position /NUN[uf]/ (space.urdu <xAdvance=100>) /HAYCi/;
		Position [REf2 REf3] (ALIFu1 <xAdvance=200>) [BEi11 TEi11];
	} UseMarkFilteringSet /[sdt]db/;
};
