LoadAnchors;
LoadPlugin qalamTools.CopyAnchors;
CopyAnchors SHADDA SHADDA_DAMMA;
CopyAnchors SHADDA SHADDA_KASRA;
CopyAnchors SHADDA SHADDA_FATHA;
CopyAnchors SHADDA SHADDA_LONG_A;

LoadPlugin qalamTools.QuantizeAnchors;
LoadPlugin qalamTools.DotAvoidance;
LoadPlugin qalamTools.NastaliqKerning;

AddSpacedAnchors 220;

Include "shared.fez";


Feature curs {
  Routine CursiveAttachment { Attach &entry &exit cursive; } IgnoreMarks RightToLeft;
};

QuantizeAnchors 10;

Feature mark {
  Routine DoMarkBase {
    Attach &top &_top bases;
    Attach &top.one &_top.one bases;
    Attach &top.two &_top.two bases;
    Attach &inside &_inside bases;
    Attach &bottom &_bottom bases;
    Attach &bottom.one &_bottom.one bases;
    Attach &bottom.two &_bottom.two bases;
    Attach &comma &_comma bases;
    Attach &comma.one &_comma.one bases;
  };
};

Routine DropOne {Substitute [sdb ddb tdb haydb] -> $1.one; };
Routine DropTwo {Substitute [sdb ddb tdb] -> $1.two; };

# Positioning rules happen *after* dot avoidance, so need to match .one and .two forms
Routine OpenSpaceBeforeKern {
  Position (@inits <xAdvance=+300>) /tdb|haydb/;
  Position (@inits <xAdvance=+200>) /sdb|ddb/;
  Position toeda (@inits <xAdvance=+200>);
};

Routine OpenSmallSpaceBeforeKern {
  Position (@inits <xAdvance=+150>);
};


Routine DropATinyBitMore {
  Position (/tdb/ <yPlacement=-40>);
};

Routine DoNothing {
  Substitute JIMi1 -> JIMi1;
};

DefineClass @bigbowlfina = /(AIN|CH_YE|JIM|LAM|NUN|QAF|SAD|SAD)[fu]/;

Routine AntiKern0sub {
  Chain (/REf/ /BEi|JIMi/ [sdb ddb tdb] ^DropOne);
} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AntiKern200sub {
  Chain (/NUN|SIN|LAM/ [sdb ddb tdb haydb] /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/NUN|SIN|LAM/ /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/(NUN|SIN|LAM)[fu]/ /BEi/ ^DoNothing sdb); # Do not drop here, it's fine
  Chain (REf3 /JIMi/ sdb ^DoNothing);

  # Unfortunate double drop...
  Chain /BEi/ ddb.one (REf1 /BEi/ ddb ^DropTwo);

  Chain (/((CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM|REf3/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain ([REu1 REf1 REf3] /JIMi/ tdb ^DropTwo);

  Chain (/(RE|VAO|HAYC|DAL|BARI_YE)[fu]\d+/ /(BEi|JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [sdb ddb ddb.one tdb haydb] ^DropOne);

  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropOne);


} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AntiKern400sub {
  Chain (/NUN|SIN|LAM/ [sdb ddb tdb haydb] /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/NUN|SIN|LAM/ /(JIM)i/ ^DoNothing [sdb ddb tdb haydb]); # Do not drop here, space instead
  Chain (/(NUN|SIN|LAM)[fu]/ /BEi/ ^DoNothing sdb); # Do not drop here, it's fine
  Chain (/REf3/ /BEi/ ^DoNothing [sdb ddb tdb]); # May need height correction...

  # Unfortunate double drop...
  Chain /BEi/ ddb.one (REf1 /BEi/ ddb ^DropTwo);

  Chain (/(RE|VAO|DAL|(CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo /JIM|BE|HAYC/ [sdb ddb tdb haydb] ^DropOne);
  Chain (/(RE|VAO|DAL|(CH_YE|AIN)[fu]\d+)|NUN|SIN|LAM/ /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/HAYCu\d+/ /(BEi|JIMi|HAYCi)/ [ddb ddb.one tdb haydb] ^DropTwo);
  Chain (/(HAYC|BARI_YE)[fu]\d+/ /(BEi|JIMi|HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$)/ [ddb ddb.one tdb haydb] ^DropOne);

  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ [sdb ddb ddb.one tdb haydb] ^DropOne);


} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];



Routine AntiKern200pos {
  Chain (/.*[fu]\d+/ toeda @inits ^OpenSpaceBeforeKern);
  Chain (/NUN|SIN/ /(JIM|BE|TE)i/ ^OpenSpaceBeforeKern [sdb ddb tdb haydb]);
  Chain (/(MIM)[fu]\d+/ /(BE|TE|JIM|HAYC)i/ ^OpenSpaceBeforeKern  [sdb ddb tdb haydb]);
  Chain (/.*[fu]\d+/ /BEi/ ^OpenSpaceBeforeKern [sdb ddb tdb haydb] /JIMf/);
  Chain (/(RE|VAO|HAYC|DAL|BARI_YE)[fu]\d+/ /HAYCi3$/ ^OpenSmallSpaceBeforeKern);
  Chain (/(RE|VAO|HAYC|DAL|BARI_YE)[fu]\d+/ /HAYCi(1|2|4|5|6|7|8|9|10|11|12|13|14)$/ ^OpenSpaceBeforeKern);
  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ ^OpenSpaceBeforeKern [sdb ddb ddb.one tdb haydb]);
  Chain (/ALIF[fu]/ /(BE|JIM|HAYC)i/ ^OpenSmallSpaceBeforeKern /ddb|tdb/);
} IgnoreLigatures UseMarkFilteringSet [sdb ddb tdb haydb toeda];

Routine AntiKern400pos {
  Chain (@bariye @inits ^OpenSpaceBeforeKern);
  Chain (@bigbowlfina /(BE|JIM|HAYC)i/ ^OpenSpaceBeforeKern /sdb|ddb|tdb/);
  Chain (/ALIF[fu]/ /(BE|JIM|HAYC)i/ ^OpenSmallSpaceBeforeKern /ddb|tdb/);
  Chain ([@isols @finas] /BEi|JIMi/ [tdb.one tdb.two] ^DropATinyBitMore);
} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb/;

Feature rlig {
  AtHeight 0-200 AntiKern0sub;
  AtHeight 200-400 AntiKern200sub;
  AtHeight 400-700 AntiKern400sub;
  AtHeight 0-400 AntiKern200pos;
  AtHeight 400-600 AntiKern400pos;
  DetectAndSwap bottom;
  DetectAndSwap top;
  Routine OtherFixes {
    Substitute FEi3 (dda) ALIFf1 -> dda.one;
  };
};

Include "sources/build/fez/pre-mkmk-repositioning.fez";

Feature mkmk {
  Routine DoMarkAttachment {
    Attach &top &_top marks;
    Attach &bottom &_bottom marks;
    Attach &bottom.yb &_bottom.yb marks;
  };
};
