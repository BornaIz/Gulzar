
Routine DropOne {
  Substitute [sdb ddb tdb haydb] -> $1.one;
  Substitute [sdb.yb ddb.yb tdb.yb haydb.yb] -> $1~yb.one;
};

Routine DropTwo {
  Substitute [sdb ddb tdb] -> $1.two;
  # Substitute [sdb.yb ddb.yb tdb.yb ] -> $1~yb.two;
};

# Positioning rules happen *after* dot avoidance, so need to match .one and .two forms
Routine OpenSpaceBeforeKern {
  Position (@inits <xAdvance=+300>) /tdb|haydb/;
  Position (@inits <xAdvance=+200>) /sdb|ddb/;
  Position toeda (@inits <xAdvance=+200>);
};

Routine OpenSmallSpaceBeforeKern {
  Position (/JIMi/ <xAdvance=+250>) /tdb/;
  Position (@inits <xAdvance=+160>) /sdb|ddb|haydb|tdb/;
};

Routine OpenTinySpaceBeforeKern {
  Position (/JIMi/ <xAdvance=+150>) /tdb/;
  Position (@inits <xAdvance=+100>) /sdb|ddb|haydb|tdb/;
};

Routine DoNothing    {Substitute JIMi1 -> JIMi1; };
Routine DoNothingPos {Position (haydb.yb <xPlacement=0>); };
Routine Tighten  { Position (@inits <xAdvance=-150>); };
Routine Raise    { Position ([sdb tdb ddb] <yPlacement=+100>); };
Routine TopRight {Position ([sdb tdb ddb] <yPlacement=+250 xPlacement=+150>); };

Routine DropATinyBitMore {
  Position ([tdb.one ddb.one tdb.yb sdb.yb ddb.yb] <yPlacement=-150>);
  Position ([tdb.two sdb.two ddb.two] <yPlacement=-220>);
};

Routine DropALotMore {
  Position ([tdb.one ddb.one tdb.yb sdb.yb ddb.yb] <yPlacement=-200>);
  Position ([tdb.two sdb.two] <yPlacement=-270>);
};



# The real stuff starts now.
DefineClass @dots = [sdb ddb tdb haydb];

Routine AtHeight0sub {
  Chain (REf1                BEi10                  ^DoNothing);

} IgnoreLigatures UseMarkFilteringSet [sdb ddb tdb];


Routine AtHeight100sub {
  Chain ([REf2 REf3]          @BEi      @dots        ^DropOne); # جر یا / سرپہ
  Chain ([REf2 REf3]          @JIMi     @dots        ^DropTwo); # ہر چیز / ہر چیز

  Chain (@dal_like            BEi3      @dots        ^DoNothing); # ربا
  Chain (@dal_like            @BEi      @dots        ^DropOne); # پرچا
  Chain (@dal_like            @JIMi     @dots        ^DropOne); # پرچا

  Chain (@vao_like            @DOTi     @dots        ^DropOne); # وپہر
  Chain (HAYCf1               @DOTi     @dots        ^DropOne); # ہہ چہر

  Chain (@kaf_like            @DOTi     @dots        ^DropOne); # یپ چل

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

Routine AtHeight200sub {
  Chain (@dal_like            HAYCi3    @dots        ^DoNothing); # رہیں
  Chain (@vao_like            BEi6      @dots        ^DropTwo); #  کر یہُو
  Chain (@dal_like            @DOTi     @dots        ^DropOne); # دچھ
  Chain (@dal_like    /FEi/   /(BE|JIM)m/ @dots        ^DropOne); # رفین
  Chain (@dal_like    @BEi    /(BE|JIM)m/ @dots        ^DropOne); # رنیست

  Chain (@vao_like            BEisd1    @dots        ^DoNothing); # وپید
  Chain (@vao_like            BEi6      @dots        ^DropTwo); # کر یہُو
  Chain (@vao_like            @BEi      @dots        ^DropOne); # ھو یں
  Chain (@vao_like            @JIMi     @dots        ^DropOne); # ھو چں
  Chain (@vao_like            HAYCi11   @dots        ^DropOne); # ساتھ ہی
  Chain (@vao_like            @DOTi     /.yb$/        ^DropTwo); # بلوچی

  Chain ([REf2 REf3]          BEi3  @DOTm @dots      ^DoNothing); # بغیر ٹیبلٹ
  Chain ([REf2 REf3]          JIMi1 @DOTm @dots      ^DoNothing); # سرخیوں
  Chain ([REf2 REf3]          BEisd1 @DOTm @dots     ^DoNothing); # سرٹیفکیٹ 
  Chain ([REf2 REf3]          @DOTi @DOTm @dots      ^DropOne); # نٹرنیٹ

  Chain ([REf2 REf3]          @JIMi     sdb          ^DoNothing); # but not سر جہا
  Chain ([REf2 REf3]          BEi12    @dots         ^DropOne); # جھر پو
  Chain ([REf2 REf3]          @DOTi     @dots        ^DropTwo); # سرپر  / سربر

  Chain (HAYCf1               @DOTi     @dots        ^DropOne); # یہ بہت

  Chain (BEf1 @dots           BEi6     @dots        ^DropOne); # جب یہُو

  Chain (@kaf_like            @JIMi     @dots        ^DropOne); # یپ چل
  Chain (@kaf_like @dots      @JIMi     @dots        ^DropOne); # یپ چل

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb tdb.yb sdb.yb ddb.yb];

Routine AtHeight400sub {
  #Chain (@dal_like            JIMi5     @dots        ^DropTwo); # پرچھا
  Chain (@dal_like            BEi3     sdb        ^DoNothing); # ر بلتی 
  Chain (@dal_like            BEisd1    @dots  @DOTm /.yb$/ ^DoNothing); # قریبی
  Chain (@dal_like            BEisd1    @dots        ^DropOne); # دیتی

  Chain (@dal_like            @DOTi     @dots ^DropTwo @DOTm @dots ^DropOne); # رچینی
  Chain (@dal_like            @DOTi     @dots        ^DropTwo); # پریھا / پرچھا / رچینی

  Chain (@vao_like            @BEi     @dots        ^DropOne); # ویکیپیڈ but not کو ہَیکل
  Chain (@vao_like            @JIMi     @dots        ^DropOne); # ویکیپیڈ

  Chain (HAYCf1               @DOTi     @dots        ^DropTwo); # ہمیشہ چلتی

  Chain (@kaf_like            @DOTi     @dots        ^DropTwo); # ک پہلو
  Chain (@kaf_like @dots      @DOTi     @dots        ^DropTwo); # سپ پہلو
  Chain (BEu1 @dots           @DOTi     @dots        ^DropTwo); # پ پہلو


} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb sdb.yb ddb.yb tdb.yb];

Routine AtHeight500sub {
  Chain (@vao_like            @DOTi     [ddb tdb]        ^DropTwo); # لو چھیا / و چھیا / but not و جھی

  Chain (@dal_like            BEi9      @dots            ^DropOne); # ریم
  Chain (@dal_like            BEi13     @dots            ^DropOne); # پریھا

  # Decisions, decisions...
  Chain (@dal_like            BEi2      @dots            ^DropOne);   # ریعقُو
  Chain (@dal_like            JIMi9     @dots            ^DropTwo);   # ر چمکتا
  Chain (@dal_like            BEi3      @dots            ^DropTwo);   # دیکھتا
  Chain (@dal_like            BEisd1    @dots            ^DropOne);   # Similar shape to دیکھتا
  Chain (@dal_like            BEi5      @dots            ^DoNothing);   # ر پھر
  Chain (@dal_like            BEi6      @dots            ^DoNothing);   # کو پہنچ
  # Chain (@dal_like            BEi5      @dots            ^DropTwo); # رپھر

  Chain (@dal_like            @DOTi     @dots            ^DoNothing); # ندبجن (space instead)

  Chain (HAYCf1               BEi5      @dots            ^DropTwo); # قہ پھیلا
  Chain (HAYCf1               BEisd1    @dots            ^DropTwo); # سلہ پیکٹس
  Chain (HAYCf1               @DOTi      @dots            ^DropOne); # لہ پیکٹس / کہ ہمیشہ

  Chain (@kaf_like            @DOTi     @dots            ^DropTwo); # تک پہنل

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];


Routine AtHeight600sub {
  Chain (@vao_like            JIMi5     @dots            ^DoNothing); # وچھی
  Chain (@vao_like            BEi6      @dots            ^DoNothing); # کو پہنچ
  Chain (@vao_like            @DOTi     [ddb tdb]        ^DropTwo);   # لو چھیا / و چھیا / but not و جھی
  Chain (@vao_like            @DOTi     @DOTm @dots  ^DropOne);       # کمیونیکیشن

  Chain (@dal_like            BEi9      @dots            ^DropTwo);   # ریم / دیمس
  Chain (@dal_like            BEi13     @dots            ^DropOne);   # پریھا

  # Decisions, decisions...
  Chain (@dal_like            BEi5      @dots            ^DoNothing);   # ر پھر
  Chain (@dal_like            BEi6      @dots            ^DoNothing);   # کو پہنچ
  # Chain (@dal_like            BEi5      @dots            ^DropTwo); # رپھر

  Chain (@dal_like            @DOTi     @dots            ^DoNothing); # ندبجن (space instead)

} IgnoreLigatures UseMarkFilteringSet [sdb ddb ddb.one tdb haydb];

# Rules which use kerning instead of dropping


Routine AtHeight200pos {
  #Chain (@bigbowlfina        [BEisd1 BEisd2] ^OpenSpaceBeforeKern);
  Chain (@bigbowlfina        @BEi   ^OpenSpaceBeforeKern); # عمل پو
  Chain (@bigbowlfina        @JIMi   ^OpenSpaceBeforeKern); # عمل پو # Not HAYCi
  Chain (@bigbowlfina        /HAYCi/   ^OpenSmallSpaceBeforeKern); # ن ہو

  Chain (@kaf_like           @DOTi   ^OpenSmallSpaceBeforeKern); # پ چل

  Chain (@dal_like           BEi3    ^OpenSmallSpaceBeforeKern   /(BE|JIM)m/ @dots); # ر نیس
  Chain (@dal_like           BEi6    /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # کر یہُو
  Chain (@dal_like           HAYCi3  ^OpenSmallSpaceBeforeKern); # رہیں?

  Chain (@mim_like           BEi12   ^OpenSpaceBeforeKern);
  Chain (@mim_like           @DOTi   ^OpenSpaceBeforeKern);

  Chain (REf1                BEi10   /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # تقریظیں
  Chain ([REf2 REf3]         BEi12   /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # جھر پو
  Chain ([REf2 REf3]         BEi3    /sdb|ddb|tdb|haydb|toeda/ ^DoNothingPos); # مر یا
  Chain ([REf2 REf3]         BEisd2  /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # تحریریں
  Chain ([REf2 REf3]         @BEi    ^OpenSmallSpaceBeforeKern    /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore @DOTm @dots); # تحریریں
  Chain ([REf2 REf3]         BEi11  ^OpenTinySpaceBeforeKern /sdb|ddb|tdb|haydb|toeda/ ^DropATinyBitMore); # عربی
  Chain ([REf2 REf3]         @BEi    ^OpenTinySpaceBeforeKern); # تحریریں !
} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb|toeda/;

Routine AtHeight400pos {
  Chain (@bigbowlfina        @DOTi   ^OpenSmallSpaceBeforeKern);  # کی پیما / کی پیمائ
  Chain ([REf2 REf3]         BEi1    ^OpenSpaceBeforeKern);  #   کیلیبریشن
  Chain ([REf2 REf3]         BEi4    ^OpenSmallSpaceBeforeKern);  #  طریقو

  Chain (@mim_like           @DOTi   ^OpenSmallSpaceBeforeKern);

  Chain (@dal_like            BEisd1 ^OpenSpaceBeforeKern @dots ^Raise @DOTm /.yb$/ ); # قریبی
  Chain (@dal_like           BEi3    ^OpenSmallSpaceBeforeKern); # ربلتی  ر بلتی 
  Chain (@dal_like           /HAYCi/ ^OpenSmallSpaceBeforeKern);       #  رہنما

  Chain (@vao_like           BEi6    ^OpenSmallSpaceBeforeKern);  #  کو پہن
  Chain (@vao_like           /HAYCi/ ^OpenSpaceBeforeKern);       # کو ہَیکل

  Chain (@alif_like          JIMi5   ^OpenSmallSpaceBeforeKern @dots);  # اچھا

} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb/;

Routine AtHeight500pos {
  Chain (@bigbowlfina        @DOTi   ^OpenSmallSpaceBeforeKern);  # کی پیچید
  Chain (@dal_like           JIMi5   ^OpenSpaceBeforeKern);       # رجھر

  Chain (@kaf_like           @BEi    ^OpenSpaceBeforeKern @dots);       # تک پہنچنے

  Chain (@mim_like           @DOTi   ^OpenSmallSpaceBeforeKern);

  Chain (@vao_like           BEi5    ^OpenSmallSpaceBeforeKern);
  Chain (HAYCf1              @BEi   ^OpenSmallSpaceBeforeKern @dots); # فہ پھیلا

} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb/;

Routine AtHeight600pos {

  # XXX  @DOTm
  Chain (@vao_like           BEisd1  ^OpenSmallSpaceBeforeKern); # کمیونیکیشن

  Chain (@bigbowlfina        @DOTi   ^OpenSpaceBeforeKern);       # ی چھو

  Chain (@alif_like          @BEi    ^OpenSmallSpaceBeforeKern @dots);  # ایپلیکیشنز
  Chain (@alif_like          @JIMi   ^OpenSpaceBeforeKern @dots);  # اچھّی

  Chain (@kaf_like            JIMi5     @dots            ^TopRight); # ید چھر, getting fancy
  Chain (@kaf_like @dots      JIMi5     @dots            ^TopRight); # 

  Chain (@dal_like           BEi5    ^OpenSpaceBeforeKern);       # ر پھر
  Chain (@dal_like           BEi7    ^OpenSpaceBeforeKern);       # ریخی
  Chain (@dal_like           BEi9    ^DoNothingPos);              # فدیم
  Chain (@dal_like           @BEi    ^OpenSmallSpaceBeforeKern);  # ندبجن /  دیکھیں / ریسپشن

  Chain (@dal_like           JIMi5   ^OpenSpaceBeforeKern);       # ورجھر

  Chain (@mim_like           @DOTi   ^OpenSmallSpaceBeforeKern);  # م ہم

  Chain (@vao_like           HAYCi9  ^OpenSpaceBeforeKern);       # وہم
  Chain (@vao_like           JIMi5   ^OpenSpaceBeforeKern) tdb;   # وچھی
  Chain (@vao_like           JIMi5   ^OpenSmallSpaceBeforeKern);  # و جھی
  Chain (@vao_like           /MIMi/  ^OpenSmallSpaceBeforeKern);  # جومُجھ
  Chain (@vao_like           BEi6    ^OpenSmallSpaceBeforeKern);  # کو پہنچ
  Chain (@vao_like           BEi5                                 /sdb|ddb|tdb|haydb/ ^DropATinyBitMore); # وپھی
  Chain (@vao_like           BEi7                                 /sdb|ddb|tdb|haydb/ ^DropATinyBitMore); # ویجٹس

  Chain (REf3                BEi5    ^OpenSpaceBeforeKern);       # سرپھر

  Chain (@kaf_like           BEi6    ^Tighten);                   # تک پہنچن
  Chain (@kaf_like           JIMi5  /sdb|ddb|tdb|haydb/ ^DropATinyBitMore); # ک چھو
  Chain (HAYCf1              /HAYCi/ ^OpenSpaceBeforeKern);       # گ ہم

  Chain (HAYCf1              BEi5    ^OpenSmallSpaceBeforeKern @dots); # قہ پھیلا
  Chain (HAYCf1              JIMi5   @dots            ^Raise);     # صفحہ چھو
  Chain (HAYCf1              HAYCi3  haydb            ^DoNothingPos); # 

} IgnoreLigatures UseMarkFilteringSet /sdb|ddb|tdb|haydb/;

Feature rlig {
  #AtHeight 0-99 AtHeight0sub;
  AtHeight 100-199 AtHeight100sub;
  AtHeight 200-399 AtHeight200sub;
  AtHeight 400-499 AtHeight400sub;
  AtHeight 500-599 AtHeight500sub;
  AtHeight 600-800 AtHeight600sub;
  AtHeight 200-399 AtHeight200pos;
  AtHeight 400-499 AtHeight400pos;
  AtHeight 500-599 AtHeight500pos;
  AtHeight 600-800 AtHeight600pos;
  DetectAndSwap top reverse;
  DetectAndSwap bottom;
  DetectAndSwap top;
  Routine OtherFixes {
    Substitute FEi3 (dda) ALIFf1 -> dda.one;
    Substitute /FEi/ sda /FEm/ (dda.one) -> dda.two;
    Substitute /FEi/ dda /FEm/ (sda) -> sda.two;
  };
};
