Include "shared.fez";
LoadPlugin qalamTools.SeparateConsecutive;

# These are a more ad-hoc collection of positioning rules.
# We put them in mkmk because we want them to be processed
# at the end, after anchor positioning is done.

DefineClass @narrowmedi = /.*m\d+$/  & (run < 350);

# Routines to move final jim dots around, to help fit sequences like
# ہپچ
Routine FinaJimTopRight {
  Position ([sdb.two ddb.two tdb.two] <yPlacement=-700>);
  Position ([sdb.one ddb.one tdb.one] <yPlacement=-600>);
  Position ([haydb.one haydb.two] <yPlacement=-300>);
  Position ([sdb ddb tdb] <xPlacement=+80 yPlacement=+80>);
  Position ([haydb] <xPlacement=+80 yPlacement=+80>);
};

Routine FinaJimDownABit {
  Position (/sdi|tdi/ <yPlacement=-80 xPlacement=-50>);
};

Routine FinaQafDownRight {
  Position ([tdb] <yPlacement=-280 xPlacement=40>);
  Position ([sdb ddb] <yPlacement=-280 xPlacement=40>);
};

Routine FinaQafUpLeft {
  Position ([tdb] <xPlacement=-120 yPlacement=+80>);
};

Feature kern {
  # Sequences like پی have problems - where is the right place to
  # put the dots? We tuck them down out of the way.
  Routine GiveChYeDotsMoreSpace {
    Position [sdb ddb tdb] ALIFf1 [BEi11 TEi11 LAMi11 LAMi7 LAMi2 LAMi9] (sdb.yb <yPlacement=-150>) [CH_YEf1 CH_YEf2 NUNf1]; # یابی

    Position ( [ddb.yb tdb.yb] <200 -180 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position [REf2 REf3] @inits ( sdb.yb <180 -180 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position [sdb.one tdb.one ddb.one] @DOTm ( [ddb.yb sdb.yb] <0 -250 0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( sdb.yb <205 -50  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <150 -50  0 0> ) [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /.yb|tdb|ddb$/ <50 0 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <80 -120 0 0> ) @narrowmedi [CH_YEf1 CH_YEf2 NUNf1];
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) @medis JIMf1;
    Position ( /haydb/ <xPlacement=50 yPlacement=-25> ) JIMf1;
  } UseMarkFilteringSet /haydb|sdb.(one|yb)|tdb|ddb|haydb/;

  DefineClass @WideFinals = @finas & (xMin < 50);

  Routine Various1 {
    # Finesse Beh Haya
    Position /BE(sd)?i/ ([ddb tdb] <xPlacement=50>) /HAYAm/;

    # Finesse Beh JIM
    Position ( /ddb|tdb/ <xPlacement=+20> ) /JIMm/;
  } UseMarkFilteringSet /tdb|ddb$/;

  Routine KernBeHaya {
    Position (space <xAdvance=-100> ) /[BT]Ei/ /HAYA/;
  } IgnoreMarks;

  DefineClass @NotTooHighStuff = (@finas|@isols) & (yMax < 600);
  DefineClass @NarrowMedials = /[mf](sd)?\d+$/ & (run < 400);
  DefineClass @NarrowInits = /i(sd)?\d+$/ & (run < 400);

  Routine FinaJimFixes {
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials  JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);

    #Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight @NarrowMedials JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight  JIMf1 /tdi|sdi/ ^FinaJimDownABit);
    Chain /(HAYC)[mi]/ (/haydb/ ^FinaJimTopRight JIMf1 /tdi|sdi/ ^FinaJimDownABit);

    # Put AIN/QAF here too since it's basically the same
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials AINf1);
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight @NarrowMedials /sdi|tdi|ddb|sdb|tdb|haydb/ ^FinaJimTopRight AINf1);
    #Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaJimTopRight AINf1);
    Chain /(JIM|BE)[mi]/ (/sdb|ddb|tdb/ ^FinaQafDownRight QAFf1);
  } UseMarkFilteringSet /sdi|tdi|ddb|sdb|tdb|haydb/;

  Routine Various2 {
    # GrossKasraAvoidanceThing
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-200> ) AINf1;
    Position ( KASRA.yb <yPlacement=+200> ) AINf1;
    Position [sdb.yb ddb.yb tdb.yb] ( KASRA.yb <yPlacement=-400> ) JIMf1;

    # Add space around pointy damma
    Position @NotTooHighStuff DAMMA (space <xAdvance=+50> );

    # Finess Init kasra yeh barree e.g. رِیے
    Position KASRA ( @inits <xAdvance=+200> ) /.yb/;

    # Clear Alif Madda
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+100> ) HAMZA_ABOVE;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+100> ) [dda tda toeda];
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+75> ) @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+75> ) @below_dots @all_above_marks;
    Position ALIFu1 MADDA ( /[ui](sd)?\d/ <xAdvance=+75> );

    # Heh Alif
    Position HAYCi3 (haydb <yPlacement=+159>) ALIFf1;
    # Clear Alif Nun KL
    Position /ALIF[uf]1/ (BEi8 <xAdvance=+100>) /sda|dda/;

    # Lam Khah
    Position /LAM[im]/ /JIM[mf]/ ([sda sda.one] <xPlacement=-50>);

    # Just fiddling now
    Position  [parenright.latin parenright.urdu] (/[BT]Ei/ <xAdvance=+70>) [dda tdb];

    # چنا 
    Position JIMi1 @nuktas /BEm3|TEm3/ ([sda dda tda] <yPlacement=+100>);
    Position JIMi1 /BEm3|TEm3/ ([sda dda tda] <yPlacement=+100>);

    # ہا
    Position (haydb <yPlacement=+50>) ALIFf1;

    #  پڑتا
    #Position REf1 toeda ([@inits @isols] <xAdvance=+200>);
    #Position REf1 sda ([@inits @isols] <xAdvance=+75>);

    Position [@dal_like @vao_like] [toeda sda dda tda HAMZA_ABOVE] ([@dal_like @vao_like @BEi] <xAdvance=+100> [sda dda tda HAMZA_ABOVE toeda]);
    Position DAMMA ([@dal_like @vao_like @inits] <xAdvance=+100> [dda tda HAMZA_ABOVE]);

    Position @alif_like (/TEi/ <xAdvance=+75>);
    Position @alif_like (/BEi/ <xAdvance=+75>) HAMZA_ABOVE;

    # لتا
    Position /(KAF|GAF|LAM)[im]/ /TEm/ (dda <yPlacement=+250>) [@alif_like /(KAF|GAF|LAM)[mf]/];
    Position /(KAF|GAF|LAM)[im]/ /TEm/ (dda <xPlacement=-50>);
  };

  Routine ClashesToRight {
    Position LAMi4 FEm11 (dda <xPlacement=-30>);
    Position HAYCi7 JIMm11 (SHADDA <yPlacement=+50>);

    # فت
    Position [FEi2 FEm2] [TEm3 TEm11] (dda <xPlacement=-20>) ALIFf1;
    Position [FEi2 FEm2] [sda ddb] [TEm3 TEm11] (dda <xPlacement=-20>) ALIFf1;
    Position [FEi2 FEm2] [TEm3 TEm11] (dda <xPlacement=0>) /[GK]AF[mf]/;
    Position [FEi2 FEm2] [sda dda] [TEm3 TEm11] (dda <xPlacement=0>) /[GK]AF[mf]/;
    Position [FEi2 FEm2] [TEm3 TEm11] (dda <xPlacement=-50>);
    Position [FEi2 FEm2] [sda dda] [TEm11 TEm3] (dda <xPlacement=-50>);
    Position @alif_like (BEisd1 <xAdvance=+50>) HAMZA_ABOVE;
    Position @alif_like (BEisd1 <xAdvance=+50>) HAMZA_ABOVE;

  } UseMarkFilteringSet [ sda dda SHADDA HAMZA_ABOVE];

  Routine DotsUnderGaf {
    Position (/\.one$/ <yPlacement=-220>) @NarrowMedials @all_above_marks /KAF|GAF/;
    Position /\.one$/ @NarrowMedials (@all_above_marks <yPlacement=-40>) /KAF|GAF/;
    Position (/\.one$/ <yPlacement=-220>) @all_above_marks /KAF|GAF/;

    Position [sda dda tda toeda] (DAMMA <yPlacement=+120>) /[KG]AF[mf]/;
    Position ([sda dda tda] <yPlacement=-50>) @BEm [sda dda tda] /[KG]AF[mf]/;
    Position ([sda dda tda] <yPlacement=-50>) @BEm  /[KG]AF[mf]/;
    Position TEm7 ([sda dda tda] <yPlacement=-70>) /JIMm/ /[KG]AF[mf]/; # مستحکم
    # Let's clear it
    Position /FE[im]/ (/sda|dda/ <yPlacement=+380>) /(KAF|GAF)m1[36]/;
    Position MADDA (/KAF|GAF/ <xAdvance=+70>);
    } UseMarkFilteringSet /toeda|tda|DAMMA|sda|dda|MADDA/;

  DefineClass @TallAndStraight = /LAM[im]|[KG]AF[im]|JIM[im]/;
  DefineClass @LowNotTeh = /^(?!TE|LAM|[KG]AF|ALIF)\w+[mf]\d+/;
  DefineClass @NarrowTeh = /^TE[mf](sd)?\d+/ & (run < 300);
  DefineClass @TallFinal = /LAM[mf]|[KG]AF[mf]|JIM[mf]|ALIFf1/;
  Routine HighTehFinesse {
    Position @TallAndStraight /TEm|JIMm/ ( [sda tda dda] <xPlacement=-150> ) /_YEf/;
  }  UseMarkFilteringSet [sda dda tda];

  Routine TdbAinFinesse {
    Position (tdb.yb <xPlacement=-30>) AINf1;
  } UseMarkFilteringSet [tdb.yb];

  Routine FeTeFinesse {
    # Position /FEi/ /TEm/ ( dda <xPlacement=-50 yPlacement=+50> ) ;
    Position /FEi/ dda /TEm/ ( dda <xPlacement=-50  yPlacement=+50> ) ;
    # قوتو
    Position dda ([VAOf1 REf1 DALf1] /TEi/ <xAdvance=+50> dda <xPlacement=-50>);
  } UseMarkFilteringSet [dda];

  Routine AddIsolDammaSpace {
    Position [@isols @finas] DAMMA ( @inits <xAdvance=+100> );
  } UseMarkFilteringSet [DAMMA];

  Routine MoreKerningFixes {
    Position REf2 (TEi12 <xAdvance=+100>); # Particular nasty case: 
    Position /ALIF/ (MIMi7 <xAdvance=+150>);
  } IgnoreLigatures UseMarkFilteringSet /\.yb/;

  Routine Various3 {
    # InitChechFinesse
    Position ( /JIMi/ <xAdvance=+50> tdb <xPlacement=+50> );
    # AvoidZahIsol
    Position [REf2 REf3] [sda tda toeda] (@isols <xAdvance=+50>);
  };
};
